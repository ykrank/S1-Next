apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

group = 'com.github.ykrank.AndroidTools'

android {
    namespace 'com.github.ykrank.androidtools'
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.1.0"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        consumerProguardFiles 'proguard-rules.pro'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    buildFeatures {
        dataBinding true
        buildConfig true
    }

    defaultPublishConfig "normalRelease"

    flavorDimensions "market"
    productFlavors {
        play {
            dimension "market"
        }
        normal {
            dimension "market"
        }
    }
}

kapt {
    mapDiagnosticLocations = true
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.javaDirectories
}

artifacts {
    archives sourcesJar
}

dependencies {
    normalApi fileTree(dir: 'libs/normal', include: ['*.jar'])
    playApi fileTree(dir: 'libs/play', include: ['*.jar'])
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    //For jackson
    api "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.1'

    api "com.google.android.material:material:1.6.0"
    api "androidx.cardview:cardview:1.0.0"
    api "androidx.percentlayout:percentlayout:1.0.0"
    api "androidx.legacy:legacy-preference-v14:1.0.0"
    api "androidx.legacy:legacy-support-v4:1.0.0"
    api "androidx.lifecycle:lifecycle-livedata-ktx:2.5.1"
    api 'androidx.constraintlayout:constraintlayout:2.1.4'

    api 'androidx.multidex:multidex:2.0.1'

    debugApi 'com.squareup.leakcanary:leakcanary-android:2.10'
    releaseApi 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.3'
    api 'org.apache.commons:commons-lang3:3.12.0'
    api 'com.google.guava:guava:31.0.1-android'

    api 'io.reactivex.rxjava2:rxjava:2.2.21'
    api 'io.reactivex.rxjava2:rxandroid:2.1.1'
    api 'io.reactivex.rxjava2:rxkotlin:2.4.0'

    api 'com.github.bumptech.glide:glide:4.14.2'
    api 'com.github.bumptech.glide:okhttp3-integration:4.14.2'
    api 'com.squareup.okhttp3:okhttp:5.0.0-alpha.2'

    api 'com.shizhefei:LargeImageView:1.1.0'
    api 'com.github.ykrank:androidautodispose:0.4.0'
    api 'com.hannesdorfmann:adapterdelegates3:3.1.0'

    playApi 'com.tencent.bugly:crashreport:4.1.9'
    normalApi 'com.tencent.bugly:crashreport_upgrade:1.6.1'

    api 'com.orhanobut:logger:2.2.0'

    //FilePicker
    api 'com.nononsenseapps:filepicker:4.2.1'

    //PictureSelector
    api 'io.github.lucksiege:pictureselector:v3.0.8'
    api 'io.github.lucksiege:compress:v3.10.7'

    //PictureUpload
    api 'org.jsoup:jsoup:1.15.3'
    api 'com.squareup.retrofit2:retrofit:2.9.0'
    api 'com.squareup.retrofit2:adapter-rxjava2:2.9.0'
    api 'com.squareup.retrofit2:converter-jackson:2.9.0'
    api 'com.squareup.retrofit2:converter-scalars:2.9.0'
}

if (android.productFlavors.size() > 0) {
    android.libraryVariants.all { variant ->
        if (variant.name.toLowerCase().contains("debug")) {
            return
        }

        def bundleTask = tasks["bundle${variant.name.capitalize()}Aar"]

        artifacts {
            archives(bundleTask.archivePath) {
                classifier variant.flavorName
                builtBy bundleTask
                name = project.name
            }
        }

    }
}
