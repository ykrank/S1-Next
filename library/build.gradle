apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

group='com.github.ykrank.AndroidTools'

android {
    compileSdkVersion 28
	buildToolsVersion rootProject.ext.buildToolsVersion
	
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.1.0"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        consumerProguardFiles 'proguard-rules.pro'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }

    defaultPublishConfig "normalRelease"

    flavorDimensions "market"
    productFlavors {
        play{
            dimension "market"
        }
        normal{
            dimension "market"
        }
    }
}

kapt {
    mapDiagnosticLocations = true
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.javaDirectories
}

artifacts {
    archives sourcesJar
}

dependencies {
    normalApi fileTree(dir: 'libs/normal', include: ['*.jar'])
    playApi fileTree(dir: 'libs/play', include: ['*.jar'])
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    //For jackson
    api "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.0'

    api "com.google.android.material:material:1.3.0-alpha02"
    api "androidx.cardview:cardview:1.0.0"
    api "androidx.percentlayout:percentlayout:1.0.0"
    api "androidx.legacy:legacy-preference-v14:1.0.0"
    api "androidx.legacy:legacy-support-v4:1.0.0"
    api "androidx.lifecycle:lifecycle-livedata:2.2.0"
    api 'androidx.constraintlayout:constraintlayout:2.0.1'

    api 'androidx.multidex:multidex:2.0.1'

    debugApi 'com.squareup.leakcanary:leakcanary-android:2.4'
    releaseApi 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.3'
    api 'org.apache.commons:commons-lang3:3.11'
    api 'com.google.guava:guava:29.0-android'

    api 'io.reactivex.rxjava2:rxjava:2.2.19'
    api 'io.reactivex.rxjava2:rxandroid:2.1.1'
    api 'io.reactivex.rxjava2:rxkotlin:2.4.0'

    api 'com.github.bumptech.glide:glide:4.11.0'
    api 'com.github.bumptech.glide:okhttp3-integration:4.11.0'
    api 'com.squareup.okhttp3:okhttp:4.9.0'

    api 'com.shizhefei:LargeImageView:1.1.0'
    api 'com.github.ykrank:androidautodispose:0.4.0'
    api 'com.hannesdorfmann:adapterdelegates3:3.1.0'

    playApi 'com.tencent.bugly:crashreport:3.2.3'
    normalApi 'com.tencent.bugly:crashreport_upgrade:1.4.2'

    api 'com.orhanobut:logger:2.2.0'

    //FilePicker
    api 'com.nononsenseapps:filepicker:4.1.0'

    //PictureSelector
    compileOnly 'com.github.LuckSiege.PictureSelector:picture_library:v2.5.8'

    //PictureUpload
    compileOnly 'org.jsoup:jsoup:1.13.1'
    compileOnly 'com.squareup.retrofit2:retrofit:2.9.0'
    compileOnly 'com.squareup.retrofit2:adapter-rxjava2:2.9.0'
    compileOnly 'com.squareup.retrofit2:converter-jackson:2.9.0'
    compileOnly 'com.squareup.retrofit2:converter-scalars:2.9.0'
}

if (android.productFlavors.size() > 0) {
    android.libraryVariants.all { variant ->
        if (variant.name.toLowerCase().contains("debug")) {
            return
        }

        def bundleTask = tasks["bundle${variant.name.capitalize()}Aar"]

        artifacts {
            archives(bundleTask.archivePath) {
                classifier variant.flavorName
                builtBy bundleTask
                name = project.name
            }
        }

    }
}
