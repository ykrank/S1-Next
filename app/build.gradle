apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'org.greenrobot.greendao'


Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def storefile = properties.getProperty('storeFile')
def storepassword = properties.getProperty('storePassword')
def keyalias = properties.getProperty('keyAlias')
def keypassword = properties.getProperty('keyPassword')

android {
    compileSdkVersion 25
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId 'me.ykrank.s1next'
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 27
        versionName '1.1.5'
        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        release {
            storeFile file(storefile)
            storePassword storepassword
            keyAlias keyalias
            keyPassword keypassword
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
    dexOptions {
        preDexLibraries = Boolean.valueOf(System.getProperty('pre-dex-libraries', 'true'))
    }
    lintOptions {
        abortOnError = Boolean.valueOf(System.getProperty('abort-on-error', 'true'))
        textReport true
        textOutput 'stdout'
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }
    buildTypes {
        debug {
            multiDexEnabled true
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
        }

        alpha {
            versionNameSuffix '-alpha'
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildTypes.each {
        it.buildConfigField "String", "DB_NAME", '"s1.db"'
    }
    productFlavors {
        play {
            manifestPlaceholders["APP_CHANNEL"] = "play.google.com"
        }
        //首发版本
        coolapk {
            manifestPlaceholders["APP_CHANNEL"] = "CoolApk"
        }
    }
    productFlavors.each {
        it.buildConfigField "String", "DB_NAME", '"s1.db"'
        it.manifestPlaceholders["INTERNAL_APP_VERSION"] = it.versionName + "-" + it.versionCode;
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
//    options.compilerArgs << '-Xlint:unchecked,deprecation'
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

greendao {
    schemaVersion 3
}

//http://stackoverflow.com/questions/28683327/how-to-check-if-gradle-dependency-has-new-version
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    playCompile fileTree(include: ['*.jar'], dir: 'libs/flavors/play')
    coolapkCompile fileTree(include: ['*.jar'], dir: 'libs/flavors/coolapk')

    annotationProcessor 'com.google.dagger:dagger-compiler:2.10'
    compile 'com.google.dagger:dagger:2.10'
    // https://github.com/google/dagger/issues/95
    provided 'javax.annotation:jsr250-api:1.0'

    retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:2.5.1'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
//    debugCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    alphaCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'

    compile "com.android.support:design:$rootProject.supportVersion"
    compile "com.android.support:cardview-v7:$rootProject.supportVersion"
    compile "com.android.support:percent:$rootProject.supportVersion"
    compile "com.android.support:preference-v14:$rootProject.supportVersion"
    compile "com.android.support:transition:$rootProject.supportVersion"
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    // https://github.com/google/guava/wiki/UsingProGuardWithGuava
    // https://github.com/google/guava/issues/2117
    // do not update
    //noinspection NewerVersionAvailable
    compile 'com.google.guava:guava:19.0'
    provided 'com.google.code.findbugs:jsr305:3.0.1'
    provided 'com.google.j2objc:j2objc-annotations:1.3'
    compile 'org.apache.commons:commons-lang3:3.5'

    compile 'com.squareup.okhttp3:okhttp-urlconnection:3.6.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.6.0'
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'
    compile 'com.squareup.retrofit2:converter-jackson:2.2.0'
    compile 'com.squareup.retrofit2:converter-scalars:2.2.0'

    compile 'com.tencent.bugly:crashreport:2.5.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.github.bumptech.glide:okhttp3-integration:1.4.0@jar'
    compile 'com.shizhefei:LargeImageView:1.0.8'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.8.5'
    compile 'com.hannesdorfmann:adapterdelegates3:3.0.2-SNAPSHOT'
    compile 'io.reactivex.rxjava2:rxjava:2.0.7'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    compile 'org.greenrobot:greendao:3.2.0'
    compile 'com.nononsenseapps:filepicker:2.5.2'

    compile 'org.jsoup:jsoup:1.10.2'

    compile 'com.bigkoo:quicksidebar:1.0.3'
    compile 'com.orhanobut:logger:1.15'
}
