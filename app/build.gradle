apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.greenrobot.greendao'


Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def _storeFile = properties.getProperty('storeFile')
def _storePassword = properties.getProperty('storePassword')
def _keyAlias = properties.getProperty('keyAlias')
def _keyPassword = properties.getProperty('keyPassword')
def httpDnsId = properties.getProperty('httpDnsId')
def httpDnsSecret = properties.getProperty('httpDnsSecret')
if(httpDnsId == null){
    httpDnsId = '""'
}
if(httpDnsSecret == null){
    httpDnsSecret = '""'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId 'me.ykrank.s1next'
        minSdkVersion 21
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 77
        versionName '2.5.3'
        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        if (_storeFile != null && !_storeFile.isEmpty()) {
            release {
                storeFile file(_storeFile)
                storePassword _storePassword
                keyAlias _keyAlias
                keyPassword _keyPassword
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        dataBinding true
    }
    dexOptions {
        preDexLibraries = Boolean.valueOf(System.getProperty('pre-dex-libraries', 'true'))
    }
    sourceSets {
        main.java.srcDirs += 'build/generated/source/greendao'
        main.java.srcDirs += 'src/main/kotlin'
    }
    lintOptions {
        abortOnError = Boolean.valueOf(System.getProperty('abort-on-error', 'true'))
        textReport true
        textOutput 'stdout'
        disable 'MissingTranslation'
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }
    buildTypes {
        debug {
            multiDexEnabled true
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
            if(signingConfigs.hasProperty('release')){
                signingConfig signingConfigs.release
            }
        }

        alpha {
            multiDexEnabled true
            applicationIdSuffix '.alpha'
            versionNameSuffix '-alpha'
            if(signingConfigs.hasProperty('release')){
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            matchingFallbacks = ['release', 'debug']
        }

        release {
            multiDexEnabled true
            if (signingConfigs.hasProperty('release')) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildTypes.each {
        it.buildConfigField "String", "DB_NAME", '"s1.db"'
        it.buildConfigField "String", "HTTP_DNS_ID", httpDnsId
        it.buildConfigField "String", "HTTP_DNS_SECRET", httpDnsSecret
    }
    flavorDimensions "market"
    productFlavors {
        play {
            dimension "market"
            manifestPlaceholders["APP_CHANNEL"] = "play.google.com"
            versionNameSuffix '-play'
        }
        normal {
            dimension "market"
            manifestPlaceholders["APP_CHANNEL"] = "normal"
        }
    }
}

kapt {
    mapDiagnosticLocations = true
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
//    options.compilerArgs << '-Xlint:unchecked,deprecation'
    options.compilerArgs << "-Xmaxerrs" << "500"
}

greendao {
    schemaVersion 7
}

//http://stackoverflow.com/questions/28683327/how-to-check-if-gradle-dependency-has-new-version
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

//    normalImplementation project(path: ':library', configuration: 'normal')
//    playImplementation project(path: ':library', configuration: 'play')
    implementation project(path: ':library')
    implementation project(path: ':JKeyboardPanelSwitch')

    implementation 'com.tencent.bugly:nativecrashreport:3.9.2'

    implementation 'com.google.dagger:dagger:2.39.1'
    kapt 'com.google.dagger:dagger-compiler:2.39.1'

    implementation 'androidx.transition:transition:1.4.1'

    implementation 'com.squareup.okhttp3:okhttp-urlconnection:4.9.2'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.9.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.9.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.9.0'

    implementation "com.github.VictorAlbertos.RxCache:runtime:1.8.3-2.x"
    implementation 'com.github.VictorAlbertos.Jolyglot:jackson:0.0.6'

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.0'
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.13.0"

    implementation 'nz.bradcampbell:paperparcel:2.0.8'
    implementation 'nz.bradcampbell:paperparcel-kotlin:2.0.8' // Optional
    implementation "nz.bradcampbell:paperparcel-api:2.0.8"
    kapt 'nz.bradcampbell:paperparcel-compiler:2.0.8'

    kapt 'com.github.bumptech.glide:compiler:4.12.0'

    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'org.greenrobot:greendao:3.3.0'
    implementation 'com.bigkoo:quicksidebar:1.0.3'

//  flipper
    releaseImplementation 'com.facebook.flipper:flipper-noop:0.114.0'
    alphaImplementation 'com.facebook.flipper:flipper-noop:0.114.0'
    debugImplementation 'com.facebook.flipper:flipper:0.114.0'
    debugImplementation 'com.facebook.soloader:soloader:0.10.1'
    debugImplementation 'com.facebook.flipper:flipper-network-plugin:0.114.0'

    implementation 'com.liulishuo.okdownload:okdownload:1.0.7'
    implementation 'com.liulishuo.okdownload:okhttp:1.0.7'
    implementation 'com.liulishuo.okdownload:sqlite:1.0.7'

    implementation('com.aliyun.ams:alicloud-android-httpdns:2.0.4@aar') {
        transitive true
    }

}
