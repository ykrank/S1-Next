apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

apply plugin: 'com.github.ben-manes.versions'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        applicationId 'cl.monsoon.s1next'
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 9
        versionName '0.5.2'
        vectorDrawables {
            generatedDensities = ['mdpi', 'hdpi', 'xhdpi', 'xxhdpi', 'xxxhdpi']
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
    dexOptions {
        preDexLibraries = Boolean.valueOf(System.getProperty('pre-dex-libraries', 'true'))
    }
    lintOptions {
        abortOnError = Boolean.valueOf(System.getProperty('abort-on-error', 'true'))
        textReport true
        textOutput 'stdout'
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:unchecked'
}

dependencies {
    compile 'com.google.dagger:dagger:2.0.2'
    apt 'com.google.dagger:dagger-compiler:2.0.2'
    // https://github.com/google/dagger/issues/95
    provided 'javax.annotation:jsr250-api:1.0'

    retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:2.1.0'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta1'

    ext.supportVersion = '23.1.1'
    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:cardview-v7:$supportVersion"
    compile "com.android.support:percent:$supportVersion"
    compile "com.android.support:preference-v14:$supportVersion"

    compile 'com.google.guava:guava:19.0'
    // https://github.com/google/guava/wiki/UsingProGuardWithGuava
    provided 'com.google.code.findbugs:jsr305:3.0.1'
    compile 'org.apache.commons:commons-lang3:3.4'

    compile 'com.squareup.okhttp3:okhttp-urlconnection:3.0.1'
    compile 'com.squareup.okhttp3:logging-interceptor:3.0.1'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.0-beta3'
    compile 'com.squareup.retrofit2:converter-jackson:2.0.0-beta3'
    compile 'com.squareup.retrofit2:converter-scalars:2.0.0-beta3'

    compile 'com.bugsnag:bugsnag-android:3.2.7'
    compile 'com.github.bumptech.glide:glide:3.6.1'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.7.0'
    compile 'com.hannesdorfmann:adapterdelegates:1.1.0'
    compile 'io.reactivex:rxjava:1.1.0'
    compile 'io.reactivex:rxandroid:1.1.0'
}
